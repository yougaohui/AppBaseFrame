apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'org.greenrobot.greendao'

def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "com.ygh.appframe"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    //greendao 数据库配置文件
    greendao {
        //指定数据库schema版本号，迁移等操作会用到
        schemaVersion 1
        //daoPackage 包名
        daoPackage 'com.ygh.appframe.dao'
        //这就是我们上面说到的自定义生成数据库文件的目录了，可以将生成的文件放到我们的java目录中，而不是build中，这样就不用额外的设置资源目录了
        targetGenDir 'src/main/java'
    }

    //执行lint检查，有任何的错误或者警告提示，都会终止构建，我们可以将其关掉。
    lintOptions {
        abortOnError false
    }

    //https://www.cnblogs.com/permanent2012moira/p/5126276.html
    //签名配置
    signingConfigs {
        debug {
        }
        release {// 自己的签名文件
            storeFile file("./doc/ygh.jks")
            storePassword "12345678"
            keyAlias "ygh"
            keyPassword "12345678"
        }
    }

    buildTypes {
        debug {
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"
            versionNameSuffix "-debug"
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }

        release {
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"
            //Zipalign优化
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            //混淆
            minifyEnabled true
            //前一部分代表系统默认的android程序的混淆文件，该文件已经包含了基本的混淆声明，后一个文件是自己的定义混淆文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //签名
            signingConfig signingConfigs.release

            //http://blog.csdn.net/u014300915/article/details/78355420
            flavorDimensions 'api'
            //渠道Flavors，配置不同风格的app,友盟渠道统计时用到
            productFlavors {
                "offical" {}
                "360" {}
                yingyongbao {}
                wandoujia {}
                baidu {}
                "91" {}
            }
            android.applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "ygh_${variant.productFlavors[0].name}_v${variant.versionName}_${releaseTime()}.apk"
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    compile 'com.android.support:design:26.1.0'
    compile 'com.android.support:cardview-v7:26.1.0'
    //网络框架
    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'com.squareup.okhttp3:logging-interceptor:3.1.2'
    compile 'com.squareup.retrofit2:converter-gson:2.0.2'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.2'
    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'io.reactivex:rxjava:1.1.0'
    //图片加载框架
    compile 'com.github.bumptech.glide:glide:3.6.0'
    compile 'com.bm.photoview:library:1.4.0'
    //数据库框架
    compile 'org.greenrobot:greendao:3.2.2'
    //greendao数据库升级框架
    compile 'com.github.yuweiguocn:GreenDaoUpgradeHelper:v1.4.0'
    //BaseRecyclerViewAdapterHelper
    compile 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.30'
    //检测内存泄漏框架
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
    //工具类框架
    compile 'com.blankj:utilcode:1.12.6'
    //本地通信框架
    compile 'org.greenrobot:eventbus:3.1.1'
    //依赖注入框架
    compile 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
}
